apply plugin: 'com.android.application'
apply plugin: 'com.bugsnag.android.gradle'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'realm-android'

def gitSha() {
    def p = 'git rev-parse --short HEAD'.execute([], project.rootDir)
    p.waitFor()
    if (p.exitValue() != 0) {
        return ""
    }
    return p.text.trim()
}

bugsnag {
    overwrite true
}

android {
    def packageName = "eu.darken.bluemusic"

    def signingPropFile = new File(System.properties['user.home'], ".appconfig/${packageName}/signing.properties")
    if (signingPropFile.canRead()) {
        Properties signingProps = new Properties()
        signingProps.load(new FileInputStream(signingPropFile))
        signingConfigs {
            release {
                storeFile new File(signingProps['release.storePath'])
                keyAlias signingProps['release.keyAlias']
                storePassword signingProps['release.storePassword']
                keyPassword signingProps['release.keyPassword']
            }
        }

        buildTypes {
            release {
                signingConfig signingConfigs.release
            }
        }
    }

    compileSdkVersion 28
    buildToolsVersion "28.0.3"

    def versionMajor = 2
    def versionMinor = 37
    def versionTag = ""

    Properties bugsnagProps = new Properties()
    def bugsnagPropsFile = new File(System.properties['user.home'], ".appconfig/${packageName}/bugsnag.properties")
    if (bugsnagPropsFile.canRead()) bugsnagProps.load(new FileInputStream(bugsnagPropsFile))

    defaultConfig {
        applicationId "${packageName}"
        minSdkVersion 19
        targetSdkVersion 28
        versionCode versionMajor * 10000 + versionMinor
        versionName "${versionMajor}.${versionMinor}${versionTag}"
        manifestPlaceholders = [apikey_bugsnag: ""]
    }
    lintOptions {
        abortOnError false
        ignore 'CheckResult'
    }
    buildTypes {
        def proguardRulesRelease = fileTree(dir: "proguard", include: ["*.pro"]).asList().toArray()
        debug {
            minifyEnabled false
            useProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt')
            proguardFiles proguardRulesRelease
            proguardFiles 'proguard-rules-debug.pro'
            //debuggable false
        }
        release {
            minifyEnabled true
            useProguard true
            proguardFiles getDefaultProguardFile('proguard-android.txt')
            proguardFiles proguardRulesRelease
            testProguardFiles getDefaultProguardFile('proguard-android.txt')
            testProguardFiles proguardRulesRelease
            manifestPlaceholders = [apikey_bugsnag: bugsnagProps.getProperty("bugsnag.apikey", "")]
        }
        applicationVariants.all { variant ->
            if (variant.buildType.name != "debug") {
                variant.outputs.each { output ->
                    output.outputFileName = applicationId + "-v" + defaultConfig.versionName + "(" + defaultConfig.versionCode + ")-" + variant.buildType.name.toUpperCase() + "-" + gitSha() + ".apk"
                }
            }
        }
    }
    flavorDimensions "default"
    productFlavors {
        mock {
            dimension = "default"
            applicationIdSuffix = ".mock"
        }
        //noinspection GroovyMissingReturnStatement
        prod {
            dimension = "default"
        }
    }

    // Remove mockRelease as it's not needed.
    android.variantFilter { variant ->
        if (variant.buildType.name.equals('release') && variant.getFlavors().get(0).name.equals('mock')) {
            variant.setIgnore(true)
        }
    }

    // Always show the result of every unit test, even if it passes.
    testOptions.unitTests.all {
        testLogging {
            events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
            outputs.upToDateWhen { false }
            showStandardStreams = true
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

tasks.withType(Test) {
    systemProperty "robolectric.logging", "stdout"
}
ext {
    daggerVersion = '2.22.1'
}

dependencies {
    // Support libs
    implementation "androidx.annotation:annotation:1.0.2"
    implementation "androidx.appcompat:appcompat:1.0.2"
    implementation "androidx.recyclerview:recyclerview:1.0.0"
    implementation "androidx.browser:browser:1.0.0"
    implementation "com.google.android.material:material:1.0.0"
    implementation "androidx.cardview:cardview:1.0.0"
    implementation "androidx.legacy:legacy-preference-v14:1.0.0"
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'

    // Crashtracking
    implementation 'com.bugsnag:bugsnag-android:4.10.0'

    // MVP lib
    implementation 'eu.darken.mvpbakery:library:0.7.1'

    // RX
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.0'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.6'

    //Dagger
    implementation "com.google.dagger:dagger:${daggerVersion}"
    kapt "com.google.dagger:dagger-compiler:${daggerVersion}"
    kaptAndroidTest "com.google.dagger:dagger-compiler:${daggerVersion}"
    androidTestImplementation 'com.google.code.findbugs:jsr305:3.0.2'
    implementation "com.google.dagger:dagger-android:${daggerVersion}"
    implementation "com.google.dagger:dagger-android-support:${daggerVersion}"

    //Butterknife
    implementation 'com.jakewharton:butterknife:10.1.0'
    kapt 'com.jakewharton:butterknife-compiler:10.1.0'

    //Timber
    implementation 'com.jakewharton.timber:timber:4.7.1'
    compileOnly 'org.jetbrains:annotations:16.0.1'

    // jUnit
    testImplementation 'junit:junit:4.12'

    // Mocktio
    androidTestImplementation 'org.mockito:mockito-core:2.24.5'
    androidTestImplementation 'com.linkedin.dexmaker:dexmaker-mockito:2.19.0'

    //Intrumentation tests (UI tests)
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0-beta01'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.2.0-beta01'

    // IAP
    implementation 'com.android.billingclient:billing:1.2.2'
}