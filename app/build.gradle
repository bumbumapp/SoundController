plugins {
    id 'com.android.application'
    id 'com.bugsnag.android.gradle'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'realm-android'
    id 'kotlin-parcelize'
}

apply plugin: 'com.getkeepsafe.dexcount'

def gitSha() {
    def p = 'git rev-parse --short HEAD'.execute([], project.rootDir)
    p.waitFor()
    if (p.exitValue() != 0) {
        return ""
    }
    return p.text.trim()
}

android {
    def packageName = "eu.darken.bluemusic"

    def signingPropFile = new File(System.properties['user.home'], ".appconfig/${packageName}/signing.properties")
    if (signingPropFile.canRead()) {
        Properties signingProps = new Properties()
        signingProps.load(new FileInputStream(signingPropFile))
        signingConfigs {
            release {
                storeFile new File(signingProps['release.storePath'])
                keyAlias signingProps['release.keyAlias']
                storePassword signingProps['release.storePassword']
                keyPassword signingProps['release.keyPassword']
            }
        }

        buildTypes {
            release {
                signingConfig signingConfigs.release
            }
        }
    }

    compileSdkVersion buildConfig.compileSdk

    def versionMajor = 2
    def versionMinor = 49
    def versionTag = ""

    Properties bugsnagProps = new Properties()
    def bugsnagPropsFile = new File(System.properties['user.home'], ".appconfig/${packageName}/bugsnag.properties")
    if (bugsnagPropsFile.canRead()) bugsnagProps.load(new FileInputStream(bugsnagPropsFile))

    defaultConfig {
        applicationId "${packageName}"
        minSdkVersion buildConfig.minSdk
        targetSdkVersion buildConfig.targetSdk
        versionCode versionMajor * 10000 + versionMinor
        versionName "${versionMajor}.${versionMinor}${versionTag}"
        manifestPlaceholders = [bugsnagApiKey: "60879455b6b7234ca04b8282247d143c"]
    }
    lintOptions {
        abortOnError false
        ignore 'CheckResult'
    }
    buildTypes {
        def proguardRulesRelease = fileTree(dir: "proguard", include: ["*.pro"]).asList().toArray()
        debug {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt')
            proguardFiles proguardRulesRelease
            proguardFiles 'proguard-rules-debug.pro'
            //debuggable false
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt')
            proguardFiles proguardRulesRelease
            testProguardFiles getDefaultProguardFile('proguard-android.txt')
            testProguardFiles proguardRulesRelease
            manifestPlaceholders = [bugsnagApiKey: bugsnagProps.getProperty("bugsnag.apikey", "")]
        }
        applicationVariants.all { variant ->
            if (variant.buildType.name != "debug") {
                variant.outputs.each { output ->
                    output.outputFileName = applicationId + "-v" + defaultConfig.versionName + "(" + defaultConfig.versionCode + ")-" + variant.buildType.name.toUpperCase() + "-" + gitSha() + ".apk"
                }
            }
        }
    }

    // Always show the result of every unit test, even if it passes.
    testOptions.unitTests.all {
        testLogging {
            events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
            outputs.upToDateWhen { false }
            showStandardStreams = true
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

tasks.withType(Test) {
    systemProperty "robolectric.logging", "stdout"
}


dependencies {
    // Support libs
    implementation "androidx.appcompat:appcompat:1.3.1"
    implementation "androidx.recyclerview:recyclerview:1.2.1"
    implementation "com.google.android.material:material:1.4.0"
    implementation "androidx.legacy:legacy-preference-v14:1.0.0"
    implementation 'androidx.constraintlayout:constraintlayout:2.1.1'

    implementation "androidx.lifecycle:lifecycle-common:2.3.1"
    implementation "androidx.lifecycle:lifecycle-common-java8:2.3.1"
    implementation "androidx.lifecycle:lifecycle-extensions:2.2.0"

    // Crashtracking
    implementation ('com.bugsnag:bugsnag-android:5.9.2') {
//        exclude group: "com.bugsnag", module: "bugsnag-plugin-android-anr"
        exclude group: "com.bugsnag", module: "bugsnag-plugin-android-ndk"
    }

    // RX
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.21'
    implementation 'com.jakewharton.rx2:replaying-share-kotlin:2.2.0'

    //Dagger
    implementation "com.google.dagger:dagger:${buildConfig.deps.dagger}"
    kapt "com.google.dagger:dagger-compiler:${buildConfig.deps.dagger}"
    kaptAndroidTest "com.google.dagger:dagger-compiler:${buildConfig.deps.dagger}"
    androidTestImplementation 'com.google.code.findbugs:jsr305:3.0.2'
    implementation "com.google.dagger:dagger-android:${buildConfig.deps.dagger}"
    implementation "com.google.dagger:dagger-android-support:${buildConfig.deps.dagger}"

    //Butterknife
    implementation 'com.jakewharton:butterknife:10.2.1'
    kapt 'com.jakewharton:butterknife-compiler:10.2.1'

    //Timber
    implementation 'com.jakewharton.timber:timber:5.0.1'

    // jUnit
    testImplementation 'junit:junit:4.13.1'

    // Mocktio
    androidTestImplementation 'org.mockito:mockito-core:3.3.3'
    androidTestImplementation 'com.linkedin.dexmaker:dexmaker-mockito:2.19.0'

    //Intrumentation tests (UI tests)
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0-alpha04'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.4.0-alpha04'

    // IAP
    implementation 'com.android.billingclient:billing:4.0.0'
}